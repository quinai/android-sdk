plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace "com.quinengine"
    compileSdk 33
    defaultConfig {
        versionCode 1
        versionName "1.0.2"
        minSdk 24
        targetSdk 33
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            kotlin.srcDirs = ['src/main/kotlin']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

task jar(type: Jar) {
    archiveClassifier.set('')
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives jar
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }

        publications {
            maven(MavenPublication) {
                from components.release

                artifactId = 'android-sdk'
                groupId = 'com.quinengine'
                version = '1.0.2'
                description = 'Android software development kit for Quin Audience Engine services.'

                artifact jar

                pom {
                    name = 'Quin Android SDK'
                    description = 'Android software deleopment kit of Quin Audience Engine Services'
                    url = 'https://www.quinengine.com'
                    packaging 'jar'

                    scm {
                        connection = 'scm:git:git://github.com/quinai/android-sdk.git'
                        developerConnection = 'scm:git:git://github.com/quinai/android-sdk.git'
                        url = 'https://github.com/quinai/android-sdk/tree/production'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'batuhan'
                            name = 'Batuhan Caliskan'
                            email = 'batuhan@quinengine.com'
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.maven
        }

    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'io.ktor:ktor-client-android:1.6.4'
    implementation 'io.ktor:ktor-client-serialization:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'
    implementation 'io.ktor:ktor-client-logging-jvm:1.6.4'
    implementation 'androidx.test.ext:junit-ktx:1.1.5'
}
